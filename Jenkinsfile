pipeline {
    agent any

    environment {
        DOCKER_COMPOSE_FILE = "docker-compose-build.yml"
        HARBOR_URL          = "192.168.3.81"
        SONAR_HOST_URL      = "http://192.168.3.81:10111"
        SONAR_PROJECT_KEY   = "dlink-apps"
    }

    stages {
        stage('Build & SonarQube Analysis') {
            steps {
                script {
                    dir('spring-app') {
                        sh "./gradlew classes"
                    }

                    withSonarQubeEnv('sonarqube') {
                        withCredentials([string(credentialsId: 'sonarqube-token', variable: 'SONAR_AUTH_TOKEN')]) {
                            sh """
                            sonar-scanner \\
                                -Dsonar.projectKey=${SONAR_PROJECT_KEY} \\
                                -Dsonar.sources=. \\
                                -Dsonar.java.binaries=\$(find . -type d -name "build" | paste -sd ",") \\
                                -Dsonar.ts.tslint.reportPaths=reports/tslint.json \\
                                -Dsonar.javascript.lcov.reportPaths=coverage/lcov.info \\
                                -Dsonar.host.url=${SONAR_HOST_URL} \\
                                -Dsonar.login=\$SONAR_AUTH_TOKEN
                            """
                        }
                    }
                }
            }
        }

        stage('Check SonarQube Quality Gate') {
            steps {
                script {
                    withCredentials([string(credentialsId: 'sonarqube-token', variable: 'SONAR_AUTH_TOKEN')]) {
                        sleep 10
                        def response = sh(
                            script: """
                                curl -u "\$SONAR_AUTH_TOKEN:" \\
                                "${SONAR_HOST_URL}/api/qualitygates/project_status?projectKey=${SONAR_PROJECT_KEY}"
                            """,
                            returnStdout: true
                        ).trim()

                        def json = readJSON(text: response)
                        def qualityGateStatus = json.projectStatus.status

                        if (qualityGateStatus != "OK") {
                            error "‚ùå Pipeline failed due to Quality Gate failure: ${qualityGateStatus}"
                        } else {
                            echo "‚úÖ Quality Gate passed successfully!"
                        }
                    }
                }
            }
        }

        stage('Detect & Build Changed Applications from docker-compose-build.yml') {
            steps {
                script {
                    // (1) docker-compose-build.ymlÏóêÏÑú ÎπåÎìúÎê† Ïù¥ÎØ∏ÏßÄ ÌÉúÍ∑∏ Ï∂îÏ∂ú
                    def composeContent = readFile(DOCKER_COMPOSE_FILE)

                    echo "üîç composeContent ÎÇ¥Ïö©:\n${composeContent}" // ÌååÏùº Ï†ÑÏ≤¥ ÌôïÏù∏

                    def servicesToBuild = []
                    def pattern = ~/image:\s*${HARBOR_URL}\/dlink\/([^:]+):([\w\.]+)/

                    // (2) `image:`Í∞Ä ÏûàÎäî ÎùºÏù∏Îßå ÌïÑÌÑ∞ÎßÅ
                    composeContent.eachLine { line ->
                        def matcher = (line =~ pattern)
                        if (matcher) {
                            def serviceName = matcher[0][1]
                            def versionTag = matcher[0][2]

                            echo "‚úÖ Îß§Ïπ≠Îê®: ÏÑúÎπÑÏä§=${serviceName}, Î≤ÑÏ†Ñ=${versionTag}"

                            servicesToBuild.add(serviceName)
                        } else {
                            echo "‚ùå Îß§Ïπ≠ ÏïàÎê®: ${line}"
                        }
                    }

                    // (3) Ï§ëÎ≥µ Ï†úÍ±∞ Î∞è ÏµúÏ¢Ö ÎπåÎìúÌï† ÏÑúÎπÑÏä§ ÌôïÏù∏
                    servicesToBuild = servicesToBuild.unique()
                    if (servicesToBuild.isEmpty()) {
                        echo "üöÄ No services need to be built. Skipping."
                        currentBuild.result = 'SUCCESS'
                        return
                    }

                    env.SERVICES_TO_BUILD = servicesToBuild.join(" ")
                    echo "üõ†Ô∏è Services to build: ${env.SERVICES_TO_BUILD}"

                    // (4) Ïã§Ï†ú Docker build Ïã§Ìñâ
                    sh "docker compose -f ${DOCKER_COMPOSE_FILE} build ${env.SERVICES_TO_BUILD}"
                }
            }
        }

        stage('Login & Push Changed Applications') {
            when {
                expression { env.SERVICES_TO_BUILD && env.SERVICES_TO_BUILD.trim() != "" }
            }
            steps {
                script {
                    // 1) Harbor Î°úÍ∑∏Ïù∏
                    withCredentials([usernamePassword(credentialsId: 'harbor-access', usernameVariable: 'HARBOR_USER', passwordVariable: 'HARBOR_PASS')]) {
                        sh "docker login ${HARBOR_URL} -u ${HARBOR_USER} -p ${HARBOR_PASS}"
                    }
                    // 2) Ïù¥ÎØ∏ÏßÄ Ìë∏Ïãú
                    withDockerRegistry([credentialsId: 'harbor-access', url: "https://${HARBOR_URL}"]) {
                        sh "docker compose -f ${DOCKER_COMPOSE_FILE} push ${env.SERVICES_TO_BUILD}"
                    }
                }
            }
        }

        stage('Update Manifests in dlink-manifests') {
            when {
                expression { env.SERVICES_TO_BUILD && env.SERVICES_TO_BUILD.trim() != "" }
            }
            steps {
                script {
                    // (1) docker-compose-build.ymlÏóêÏÑú ÎπåÎìúÎêú Ïù¥ÎØ∏ÏßÄ ÌÉúÍ∑∏ Îã§Ïãú ÌååÏã±
                    def composeContent = readFile(DOCKER_COMPOSE_FILE)
                    echo "üîç composeContent ÎÇ¥Ïö©:\n${composeContent}" // ‚≠ê ÌååÏùº Ï†ÑÏ≤¥ ÎÇ¥Ïö© Ï∂úÎ†•

                    def versionMap = [:]
                    composeContent.eachLine { line ->
                        def matcher = line =~ /image:\s*${HARBOR_URL}\/dlink\/([^:]+):([\w\.]+)/
                        if (matcher) {
                            def serviceName = matcher[0][1]
                            def versionTag = matcher[0][2]
                            echo "‚úÖ Îß§Ïπ≠Îê®: ÏÑúÎπÑÏä§=${serviceName}, Î≤ÑÏ†Ñ=${versionTag}"  // ‚≠ê Îß§Ïπ≠Îêú Í∞í Ï∂úÎ†•

                            versionMap[serviceName] = versionTag
                        }
                    }

                    // (2) ÏÑúÎπÑÏä§Î™Ö -> Ìå®ÏπòÌååÏùº Îß§Ìïë
                    def patchMap = [
                        "api-gateway":      "gateway-patch.yaml",
                        "auth-service":     "auth-patch.yaml",
                        "alcohol-service":  "alcohol-patch.yaml",
                        "highball-service": "highball-patch.yaml",
                        "review-service":   "review-patch.yaml",
                        "pairing-service":  "pairing-patch.yaml",
                        "next-app":         "next-patch.yaml"
                    ]

                    // (3) Git clone & checkout
                    withCredentials([usernamePassword(credentialsId: 'github-access', usernameVariable: 'GIT_USER', passwordVariable: 'GIT_PASS')]) {
                        sh """
                        rm -rf dlink-manifests
                        git clone https://\$GIT_USER:\$GIT_PASS@github.com/ACS7th/dlink-manifests.git
                        cd dlink-manifests
                        git checkout staging
                        git config user.name "dealim"
                        git config user.email "dealimmmm@gmail.com"
                        """

                        // (4) Î≥ÄÍ≤ΩÎêú ÏÑúÎπÑÏä§Îì§Ïùò image ÌÉúÍ∑∏ ÏóÖÎç∞Ïù¥Ìä∏
                        env.SERVICES_TO_BUILD.split(" ").each { service ->
                            def patchFile      = patchMap[service]
                            def currentVersion = versionMap[service]
                            echo "Current version for service '${service}': ${currentVersion}"

                            if (patchFile && currentVersion) {
                                sh """
                                sed -i 's|image: ${HARBOR_URL}/dlink/${service}:.*|image: ${HARBOR_URL}/dlink/${service}:${currentVersion}|' dlink-manifests/overlays/production/patches/${patchFile}
                                """
                            } else {
                                echo "No patch file or version found for service: ${service}"
                            }
                        }

                        // (5) Git commit & push
                        sh """
                        cd dlink-manifests
                        git add overlays/production/patches
                        git commit -m "Update image versions for CI"
                        git push origin staging
                        """
                    }
                }
            }
        }
    }

    post {
        success {
            echo '‚úÖ Build & push to Harbor, and manifest update completed successfully!'
        }
        failure {
            echo '‚ùå Build failed. Check logs.'
        }
    }
}

